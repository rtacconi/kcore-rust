{
  config,
  lib,
  pkgs,
  ...
}: let
  cfg = config.ch-vm.vms;
  helpers = import ./helpers.nix {inherit lib;};

  bridgeName = name: "kbr-${name}";
  tapName = helpers.tapName;
  subnetPrefix = ip: let
    match = builtins.match "([0-9]+\\.[0-9]+\\.[0-9]+)\\.[0-9]+" ip;
  in
    if match == null
    then throw "invalid IPv4 address for gatewayIP: ${ip}"
    else builtins.elemAt match 0;

  netmaskToCidr = mask:
    {
      "255.0.0.0" = "8";
      "255.255.0.0" = "16";
      "255.255.128.0" = "17";
      "255.255.192.0" = "18";
      "255.255.224.0" = "19";
      "255.255.240.0" = "20";
      "255.255.248.0" = "21";
      "255.255.252.0" = "22";
      "255.255.254.0" = "23";
      "255.255.255.0" = "24";
      "255.255.255.128" = "25";
      "255.255.255.192" = "26";
      "255.255.255.224" = "27";
      "255.255.255.240" = "28";
      "255.255.255.248" = "29";
      "255.255.255.252" = "30";
    }
    .${mask}
    or (throw "unsupported netmask: ${mask}");
in {
  config = lib.mkIf cfg.enable {
    assertions =
      lib.mapAttrsToList (vmName: vmCfg: {
        assertion = cfg.networks ? ${vmCfg.network};
        message = "VM '${vmName}' references network '${vmCfg.network}' which is not defined in ch-vm.vms.networks.";
      })
      cfg.virtualMachines;

    boot.kernelModules = ["tun" "tap" "br_netfilter"];

    networking.nftables.enable = true;

    systemd.services =
      lib.mapAttrs' (
        netName: netCfg:
          lib.nameValuePair "kcore-bridge-${netName}" {
            description = "kcore bridge for network ${netName}";
            wantedBy = ["multi-user.target"];
            before =
              lib.mapAttrsToList (
                vmName: vmCfg:
                  "kcore-vm-${vmName}.service"
              ) (lib.filterAttrs (_: vm: vm.network == netName) cfg.virtualMachines);

            serviceConfig = {
              Type = "oneshot";
              RemainAfterExit = true;
            };

            path = [pkgs.iproute2 pkgs.nftables];

            script = ''
              bridge="${bridgeName netName}"
              ip link show "$bridge" >/dev/null 2>&1 && exit 0

              # Safety guard: prevent bridge subnet from hijacking the host LAN.
              ext_ip=$(ip -4 -o addr show dev "${cfg.gatewayInterface}" scope global 2>/dev/null | awk 'NR==1 {print $4}' | cut -d/ -f1)
              if [ -n "$ext_ip" ]; then
                gw_ip="${netCfg.gatewayIP}"
                ext_prefix="''${ext_ip%.*}"
                gw_prefix="''${gw_ip%.*}"
                if [ "$ext_prefix" = "$gw_prefix" ]; then
                  echo "Refusing ch-vm network '${netName}': gatewayIP ${netCfg.gatewayIP} overlaps external subnet on ${cfg.gatewayInterface} ($ext_ip)"
                  exit 1
                fi
              fi

              ip link add "$bridge" type bridge
              ip addr add ${netCfg.gatewayIP}/${netmaskToCidr netCfg.internalNetmask} dev "$bridge"
              ip link set "$bridge" up

              nft add table ip kcore-${netName} 2>/dev/null || true
              nft add chain ip kcore-${netName} postrouting '{ type nat hook postrouting priority srcnat; }'
              nft add rule ip kcore-${netName} postrouting oifname "${cfg.gatewayInterface}" masquerade
            '';

            preStop = ''
              bridge="${bridgeName netName}"
              nft delete table ip kcore-${netName} 2>/dev/null || true
              ip link set "$bridge" down 2>/dev/null || true
              ip link delete "$bridge" 2>/dev/null || true
            '';
          }
      )
      cfg.networks
      // lib.mapAttrs' (
        netName: netCfg:
          lib.nameValuePair "kcore-dhcp-${netName}" {
            description = "kcore dnsmasq DHCP for network ${netName}";
            requires = ["kcore-bridge-${netName}.service"];
            after = ["kcore-bridge-${netName}.service"];
            wantedBy = ["multi-user.target"];
            serviceConfig = {
              Type = "simple";
              Restart = "always";
              RestartSec = 2;
              ExecStartPre = "${pkgs.coreutils}/bin/mkdir -p /run/kcore";
              ExecStart = "${pkgs.dnsmasq}/bin/dnsmasq --keep-in-foreground --bind-interfaces --interface=${bridgeName netName} --except-interface=lo --dhcp-authoritative --dhcp-range=${subnetPrefix netCfg.gatewayIP}.100,${subnetPrefix netCfg.gatewayIP}.199,${netCfg.internalNetmask},12h --dhcp-option=option:router,${netCfg.gatewayIP} --dhcp-option=option:dns-server,1.1.1.1,8.8.8.8 --dhcp-leasefile=/run/kcore/dnsmasq-${netName}.leases --pid-file=/run/kcore/dnsmasq-${netName}.pid";
            };
          }
      )
      cfg.networks
      // lib.mapAttrs' (
        vmName: vmCfg:
          lib.nameValuePair "kcore-tap-${vmName}" {
            description = "TAP interface for VM ${vmName}";
            requires = ["kcore-bridge-${vmCfg.network}.service"];
            after = ["kcore-bridge-${vmCfg.network}.service"];
            before = ["kcore-vm-${vmName}.service"];
            wantedBy = ["kcore-vm-${vmName}.service"];

            serviceConfig = {
              Type = "oneshot";
              RemainAfterExit = true;
            };

            path = [pkgs.iproute2];

            script = ''
              tap="${tapName vmName}"
              ip tuntap add dev "$tap" mode tap
              ip link set "$tap" master "${bridgeName vmCfg.network}"
              ip link set "$tap" up
            '';

            preStop = ''
              ip link delete "${tapName vmName}" 2>/dev/null || true
            '';
          }
      )
      cfg.virtualMachines;

    networking.firewall.trustedInterfaces =
      lib.optional (cfg.networks != {}) "kbr-+";

    boot.kernel.sysctl."net.ipv4.ip_forward" = lib.mkDefault 1;
  };
}
